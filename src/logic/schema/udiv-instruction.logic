// http://llvm.org/docs/LangRef.html#udiv-instruction
// keywords: arithmetic; binary; instruction

udiv_instruction(Insn) -> instruction(Insn).

udiv_instruction:first_operand[Insn] = Left ->
   udiv_instruction(Insn), operand(Left).

udiv_instruction:second_operand[Insn] = Right ->
   udiv_instruction(Insn), operand(Right).


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘udiv‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema:sanity()
 , udiv_instruction(Insn)
 ->
   udiv_instruction:first_operand[Insn] = _.

schema:sanity()
 , udiv_instruction(Insn)
 ->
   udiv_instruction:second_operand[Insn] = _.

schema:sanity()
 , udiv_instruction(Insn)
 , instruction:type[Insn] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , instruction:type[Insn] = Type
 , udiv_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

schema:sanity()
 , instruction:type[Insn] = Type
 , udiv_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
