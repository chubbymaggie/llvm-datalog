// http://llvm.org/docs/LangRef.html#shl-instruction
// keywords: bitwise; binary; instruction

shl_instruction(Insn) -> instruction(Insn).

shl_instruction:first_operand[Insn] = Left ->
   shl_instruction(Insn), operand(Left).

shl_instruction:second_operand[Insn] = Right ->
   shl_instruction(Insn), operand(Right).


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘shl‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema:sanity()
 , shl_instruction(Insn)
 ->
   shl_instruction:first_operand[Insn] = _.

schema:sanity()
 , shl_instruction(Insn)
 ->
   shl_instruction:second_operand[Insn] = _.

schema:sanity()
 , shl_instruction(Insn)
 , instruction:type[Insn] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , instruction:type[Insn] = Type
 , shl_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

schema:sanity()
 , instruction:type[Insn] = Type
 , shl_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
