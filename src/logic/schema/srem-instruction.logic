// http://llvm.org/docs/LangRef.html#srem-instruction
// keywords: arithmetic; binary; instruction

srem_instruction(Insn) -> instruction(Insn).

srem_instruction:first_operand[Insn] = Left ->
   srem_instruction(Insn), operand(Left).

srem_instruction:second_operand[Insn] = Right ->
   srem_instruction(Insn), operand(Right).


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘srem‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema:sanity()
 , srem_instruction(Insn)
 ->
   srem_instruction:first_operand[Insn] = _.

schema:sanity()
 , srem_instruction(Insn)
 ->
   srem_instruction:second_operand[Insn] = _.

schema:sanity()
 , srem_instruction(Insn)
 , instruction:type[Insn] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , instruction:type[Insn] = Type
 , srem_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

schema:sanity()
 , instruction:type[Insn] = Type
 , srem_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
