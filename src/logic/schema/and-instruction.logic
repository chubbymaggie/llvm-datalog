// http://llvm.org/docs/LangRef.html#and-instruction
// keywords: bitwise; binary; instruction

and_instruction(Insn) -> instruction(Insn).

and_instruction:first_operand[Insn] = Left ->
   and_instruction(Insn), operand(Left).

and_instruction:second_operand[Insn] = Right ->
   and_instruction(Insn), operand(Right).


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘and‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema:sanity()
 , and_instruction(Insn)
 ->
   and_instruction:first_operand[Insn] = _.

schema:sanity()
 , and_instruction(Insn)
 ->
   and_instruction:second_operand[Insn] = _.

schema:sanity()
 , and_instruction(Insn)
 , instruction:type[Insn] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , instruction:type[Insn] = Type
 , and_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

schema:sanity()
 , instruction:type[Insn] = Type
 , and_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
