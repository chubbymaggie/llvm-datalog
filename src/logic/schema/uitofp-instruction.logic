// http://llvm.org/docs/LangRef.html#uitofp-to-instruction
// keywords: conversion; unary; instruction

uitofp_instruction(Insn) -> instruction(Insn).

uitofp_instruction:from[Insn] = Value ->
   uitofp_instruction(Insn), operand(Value).

uitofp_instruction:to_type[Insn] = Type ->
   uitofp_instruction(Insn), type(Type).

uitofp_instruction:from_type[Insn] = Type <-
   uitofp_instruction:from[Insn] = Value,
   operand:type[Value] = Type.


//------------------------------------------------------------------------------
// [Constraints]
//
// The ‘uitofp‘ instruction takes:
//      (i)  a value to cast, which must be a scalar or vector integer value,
//  (ii) and a type to cast it to, which must be a floating point type.
//
// If the value to be casted is a vector integer type:
//  (i)  the type must be a vector floating point type,
//  (ii) with the same number of elements.
//------------------------------------------------------------------------------

schema:sanity()
 , uitofp_instruction(Insn)
 ->
   uitofp_instruction:from[Insn] = _.

schema:sanity()
 , uitofp_instruction(Insn)
 ->
   uitofp_instruction:to_type[Insn] = _.

schema:sanity()
 , uitofp_instruction:to_type[Insn] = Type
 ->
   instruction:type[Insn] = Type.

schema:sanity()
 , uitofp_instruction:from_type[_] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , uitofp_instruction:to_type[_] = Type
 ->
   fp_type(Type); vector_type:fp(Type).

schema:sanity()
 , uitofp_instruction(Insn)
 , uitofp_instruction:from_type[Insn] = From
 , uitofp_instruction:to_type[Insn] = To
 ->
   vector:compatible(From, To).
