// http://llvm.org/docs/LangRef.html#insertelement-instruction
// keywords: vector; instruction

insertelement_instruction(Insn) -> instruction(Insn).


insertelement_instruction:base[Insn] = Vector ->
   insertelement_instruction(Insn), operand(Vector).

insertelement_instruction:index[Insn] = Index ->
   insertelement_instruction(Insn), operand(Index).

insertelement_instruction:value[Insn] = Value ->
   insertelement_instruction(Insn), operand(Value).

// The type of the elements that the vector comprises.
insertelement_instruction:type[Insn] = Comp <-
   insertelement_instruction:base[Insn] = Vector,
   operand:type[Vector] = Type,
   vector_type:component[Type] = Comp.


//------------------------------------------------------------------------------
// Constraints:
//
// The ‘insertelement‘ instruction takes three operands:
//    (i)   the first operand is a value of vector type
//    (ii)  the second operand is a scalar value whose type must equal
//          the element type of the first operand, and
//    (iii) the third operand is an index of i32-integer type.
//
// The result is a vector of the same type as the first operand.
//------------------------------------------------------------------------------

schema:sanity()
 , insertelement_instruction(Insn)
 ->
   insertelement_instruction:base[Insn] = _.

schema:sanity()
 , insertelement_instruction(Insn)
 ->
   insertelement_instruction:index[Insn] = _.

schema:sanity()
 , insertelement_instruction(Insn)
 ->
   insertelement_instruction:value[Insn] = _.

schema:sanity()
 , insertelement_instruction:base[_] = Vector
 , operand:type[Vector] = Type
 ->
   vector_type(Type).

schema:sanity()
 , insertelement_instruction:index[_] = Index
 , operand:type[Index] = Type
 ->
   int32_type[] = Type.

schema:sanity()
 , insertelement_instruction:value[Insn] = Value
 ,  operand:type[Value] = Type
 ->
   insertelement_instruction:type[Insn] = Type.

schema:sanity()
 , insertelement_instruction:base[Insn] = Vector
 , operand:type[Vector] = Type
 ->
   instruction:type[Insn] = Type.
