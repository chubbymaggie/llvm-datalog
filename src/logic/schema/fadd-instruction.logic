// http://llvm.org/docs/LangRef.html#fadd-instruction
// keywords: arithmetic; binary; instruction

fadd_instruction(Insn) -> instruction(Insn).

fadd_instruction:first_operand[Insn] = Left ->
   fadd_instruction(Insn), operand(Left).

fadd_instruction:second_operand[Insn] = Right ->
   fadd_instruction(Insn), operand(Right).


//-------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘fadd‘ instruction must be:
//      (i)  floating point or
//      (ii) vector of floating point values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//-------------------------------------------------------------------

schema:sanity()
 , fadd_instruction(Insn)
 ->
   fadd_instruction:first_operand[Insn] = _.

schema:sanity()
 , fadd_instruction(Insn)
 ->
   fadd_instruction:second_operand[Insn] = _.

schema:sanity()
 , fadd_instruction(Insn)
 , instruction:type[Insn] = Type
 ->
   fp_type(Type); vector_type:fp(Type).

schema:sanity()
 , instruction:type[Insn] = Type
 , fadd_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

schema:sanity()
 , instruction:type[Insn] = Type
 , fadd_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
