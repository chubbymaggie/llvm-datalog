// http://llvm.org/docs/LangRef.html#fptrunc-to-instruction
// keywords: conversion; unary; instruction

fptrunc_instruction(Insn) -> instruction(Insn).

fptrunc_instruction:from[Insn] = Value ->
   fptrunc_instruction(Insn), operand(Value).

fptrunc_instruction:to_type[Insn] = Type ->
   fptrunc_instruction(Insn), fp_type(Type).

fptrunc_instruction:from_type[Insn] = Type <-
   fptrunc_instruction:from[Insn] = Value,
   operand:type[Value] = Type.


//-------------------------------------------------------------------
// [Constraints]
//
// 1) The ‘fptrunc‘ instruction takes:
//      (i)  a floating point value to cast,
//      (ii) and a floating point type to cast it to.
//
// 2) The source type must be larger than the destination type.
//-------------------------------------------------------------------

schema:sanity()
 , fptrunc_instruction(Insn)
 ->
   fptrunc_instruction:from[Insn] = _.

schema:sanity()
 , fptrunc_instruction(Insn)
 ->
   fptrunc_instruction:to_type[Insn] = _.

schema:sanity()
 , fptrunc_instruction:to_type[Insn] = Type
 ->
   instruction:type[Insn] = Type.

schema:sanity()
 , fptrunc_instruction:from_type[_] = Type
 ->
   fp_type(Type).
