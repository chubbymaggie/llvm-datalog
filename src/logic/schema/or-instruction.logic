// http://llvm.org/docs/LangRef.html#or-instruction
// keywords: bitwise; binary; instruction

or_instruction(Insn) -> instruction(Insn).

or_instruction:first_operand[Insn] = Left ->
   or_instruction(Insn), operand(Left).

or_instruction:second_operand[Insn] = Right ->
   or_instruction(Insn), operand(Right).


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘or‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema:sanity()
 , or_instruction(Insn)
 ->
   or_instruction:first_operand[Insn] = _.

schema:sanity()
 , or_instruction(Insn)
 ->
   or_instruction:second_operand[Insn] = _.

schema:sanity()
 , or_instruction(Insn)
 , instruction:type[Insn] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , instruction:type[Insn] = Type
 , or_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

schema:sanity()
 , instruction:type[Insn] = Type
 , or_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
