// http://llvm.org/docs/LangRef.html#switch-instruction
// keywords: terminator; instruction

switch_instruction(Insn) -> instruction(Insn).


terminator_instruction(Insn) <-
   switch_instruction(Insn).

switch_instruction:operand[Insn] = Value ->
   switch_instruction(Insn), operand(Value).

switch_instruction:default_label[Insn] = Label ->
   switch_instruction(Insn), variable(Label).

switch_instruction:case:value[Insn, Index] = Constant ->
   switch_instruction(Insn), int[64](Index), constant(Constant).

switch_instruction:case:label[Insn, Index] = Label ->
   switch_instruction(Insn), int[64](Index), variable(Label).

switch_instruction:ncases[Insn] = Total ->
   switch_instruction(Insn), int[64](Total).


//------------------------------------------------------------------------------
// [Constraints]
//
// The ‘switch‘ instruction uses three parameters:
//  (i)   an integer comparison value ‘value‘,
//  (ii)  a default ‘label‘ destination, and
//  (iii) an array of pairs of comparison value constants
//        and ‘label‘s.
//
// The table is not allowed to contain duplicate constant entries.
//------------------------------------------------------------------------------

schema:sanity()
 , switch_instruction(Insn)
 ->
   switch_instruction:ncases[Insn] = _.

schema:sanity()
 , switch_instruction(Insn)
 ->
   switch_instruction:default_label[Insn] = _.

schema:sanity()
 , switch_instruction:default_label[_] = Label
 ->
   variable:label(Label).

schema:sanity()
 , switch_instruction:case:label[_, _] = Label
 ->
   variable:label(Label).

schema:sanity()
 , switch_instruction:operand[_] = Value
 , operand:type[Value] = Type
 ->
   integer_type(Type).

schema:sanity()
 , switch_instruction:case:value[_, _] = Constant
 , constant:type[Constant] = Type
 ->
   integer_type(Type).

schema:sanity()
 , switch_instruction(Insn)
 , switch_instruction:case:value[Insn, Index1] = Const1
 , switch_instruction:case:value[Insn, Index2] = Const2
 , constant:eq(Const1, Const2)
 ->
   Index1 = Index2.

schema:sanity()
 , switch_instruction:case:label[Insn, Index] = _
 , switch_instruction:ncases[Insn] = Total
 ->
   0 <= Index,
   Index < Total.

schema:sanity()
 , switch_instruction:case:label[Insn, Index] = _
 ->
   switch_instruction:case:value[Insn, Index] = _.

schema:sanity()
 , switch_instruction:case:value[Insn, Index] = _
 ->
   switch_instruction:case:label[Insn, Index] = _.
