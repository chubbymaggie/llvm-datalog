// http://llvm.org/docs/LangRef.html#fpext-to-instruction
// keywords: conversion; unary; instruction

fpext_instruction(Insn) -> instruction(Insn).

fpext_instruction:from[Insn] = Value ->
   fpext_instruction(Insn), operand(Value).

fpext_instruction:to_type[Insn] = Type ->
   fpext_instruction(Insn), type(Type).

fpext_instruction:from_type[Insn] = Type <-
   fpext_instruction:from[Insn] = Value,
   operand:type[Value] = Type.


//-------------------------------------------------------------------
// [Constraints]
//
// 1) The ‘fpext‘ instruction takes:
//      (i)  a floating point value to cast,
//      (ii) and a floating point type to cast it to.
//
// 2) The source type must be smaller than the destination type.
//-------------------------------------------------------------------

schema:sanity()
 , fpext_instruction(Insn)
 ->
   fpext_instruction:from[Insn] = _.

schema:sanity()
 , fpext_instruction(Insn)
 ->
   fpext_instruction:to_type[Insn] = _.

schema:sanity()
 , fpext_instruction:to_type[Insn] = Type
 ->
   instruction:type[Insn] = Type.

schema:sanity()
 , fpext_instruction:from_type[_] = Type
 ->
   fp_type(Type).

schema:sanity()
 , fpext_instruction:to_type[_] = Type
 ->
   fp_type(Type).
