// http://llvm.org/docs/LangRef.html#mul-instruction
// keywords: arithmetic; binary; instruction

mul_instruction(Insn) -> instruction(Insn).

mul_instruction:first_operand[Insn] = Left ->
   mul_instruction(Insn), operand(Left).

mul_instruction:second_operand[Insn] = Right ->
   mul_instruction(Insn), operand(Right).


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘mul‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema:sanity()
 , mul_instruction(Insn)
 ->
   mul_instruction:first_operand[Insn] = _.

schema:sanity()
 , mul_instruction(Insn)
 ->
   mul_instruction:second_operand[Insn] = _.

schema:sanity()
 , mul_instruction(Insn)
 , instruction:type[Insn] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , instruction:type[Insn] = Type
 , mul_instruction:first_operand[Insn] = Left
 ->
   operand:type[Left] = Type.

schema:sanity()
 , instruction:type[Insn] = Type
 , mul_instruction:second_operand[Insn] = Right
 ->
   operand:type[Right] = Type.
