// http://llvm.org/docs/LangRef.html#sext-to-instruction
// keywords: conversion; unary; instruction

sext_instruction(Insn) -> instruction(Insn).

sext_instruction:from[Insn] = Value ->
   sext_instruction(Insn), operand(Value).

sext_instruction:to_type[Insn] = Type ->
   sext_instruction(Insn), type(Type).

sext_instruction:from_type[Insn] = Type <-
   sext_instruction:from[Insn] = Value,
   operand:type[Value] = Type.


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The ‘sext‘ instruction takes:
//      (i)  a value to cast,
//      (ii) and a type to cast it to.
//
// 2) Both source and destination types must be:
//      (i)  integers or
//      (ii) vectors of the same number of integers.
//
// 3) The source type must be smaller than the destination type.
//------------------------------------------------------------------------------

schema:sanity()
 , sext_instruction(Insn)
 ->
   sext_instruction:from[Insn] = _.

schema:sanity()
 , sext_instruction(Insn)
 ->
   sext_instruction:to_type[Insn] = _.

schema:sanity()
 , sext_instruction:to_type[Insn] = Type
 ->
   instruction:type[Insn] = Type.

schema:sanity()
 , sext_instruction:from_type[_] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , sext_instruction:to_type[_] = Type
 ->
   integer_type(Type); vector_type:integer(Type).

schema:sanity()
 , sext_instruction(Insn)
 , sext_instruction:from_type[Insn] = From
 , sext_instruction:to_type[Insn] = To
 ->
   vector:compatible(From, To).
